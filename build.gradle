plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

group = 'fr.sandro642.github'
version = '0.4.3.1-STABLE'

// Générer une classe de version automatiquement
task generateVersionClass {
    doLast {
        def versionFile = file("$buildDir/generated/source/version/fr/sandro642/github/update/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text = """
package fr.sandro642.github.update;

public class Version {
    public static final String VERSION = "${project.version}";
}
"""
    }
}

compileJava.dependsOn generateVersionClass
sourceSets.main.java.srcDir "$buildDir/generated/source/version"

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}

repositories {
    mavenCentral()

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'io.projectreactor:reactor-core:3.6.9'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.2.2'

    // Version moderne sans problèmes de dépendances
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'

    implementation 'com.google.code.gson:gson:2.10.1'
}

application {
    mainClass = 'fr.sandro642.github.ConnectLib'
}

tasks.jar {
    manifest {
        attributes 'Main-Class': 'fr.sandro642.github.ConnectLib'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    //withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = project.group.toString()
            artifactId = 'ConnectLib'
            version = project.version.toString()
            pom {
                name = 'ConnectLib'
                description = 'A library for connecting to APIs'
                url = 'https://sandro642.github.io/connectlib'
                licenses {
                    license {
                        name = 'The MIT License, Version 2.0'
                        url = 'https://github.com/Sandro642/ConnectLib/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sandro642'
                        name = 'Sandro'
                        email = 'sandro33810@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/sandro642/ConnectLib.git'
                    developerConnection = 'scm:git:ssh://github.com/sandro642/ConnectLib.git'
                    url = 'https://github.com/sandro642/ConnectLib'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'LocalRepo'
            url = uri(layout.buildDirectory.dir('repo'))
        }
    }
}
